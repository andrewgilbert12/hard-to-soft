// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
      return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
      return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
      var char val;
      let val = 0;

      while(val = 0){
        let val = Keyboard.keyPressed();
      }

      do Output.printChar(val);
      return val;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
      var char val;
      var String str;
      let val = 0;
      let str = String.new(100);
      do Output.printString(message);

      while (~(val = String.newLine())) {
        let val = 0;

        while (val = 0) {
            let val = Keyboard.keyPressed();
        }

        if (val = String.backSpace()) {
          if (str.length() > 0) {
              do Output.backSpace();
              do str.eraseLastChar();
          }
        } else {
            do Output.printChar(val);
            if (~(val = String.newLine())) {
              do str.appendChar(val);
            }
        }

        do Sys.wait(200);
      }

      do Output.println();
      return str; 
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
      var String line;
      var int ret;

      let line = Keyboard.readLine(message);
      let ret = line.intValue();

      do line.dispose();

      return ret;
    }
}
